'' When "Send" is invoked on an email, prompt to continue if more than one external domain name found which is different to sender's
''
'' Useful to help stop accidental sending of information to incorrect recipients
''
'' Paste the contents of this file into the ThisOutlookSession object in Outlook (Alt F11) & save it
''
'' @guyrleech, 2020

Private Sub Application_ItemSend(ByVal Item As Object, Cancel As Boolean)

    Dim olNS As Outlook.NameSpace
    Dim ownDomainName, parts() As String
    Dim answer As String
    
    Set olApp = CreateObject("Outlook.Application")
    Set olNS = olApp.GetNamespace("MAPI")
    
    '' Find the domain name for the account sending the email (may be Exchange so can't use Sender property
    For Each Account In olNS.Accounts
        parts = Split(Account.SmtpAddress, "@")
        If UBound(parts) = 1 Then
            If ownDomainName = "" Then
                ownDomainName = parts(1)
            ElseIf ownDomainName <> parts(1) Then
                answer = MsgBox("Found multiple different domain names for sender - continue?", vbQuestion + vbYesNo, "Warning")
                If answer <> vbYes Then
                    Cancel = True
                End If
            End If
        End If
    Next
    
    If ownDomainName = "" Then
        MsgBox "Unable to determine sender's email domain", vbCritical + vbOKOnly, "Serious Error"
    End If
    
    Dim existing As String
    Dim domains As Collection
    Set domains = New Collection
    Dim visibleRecipients As Integer
    Dim invisibleRecipients As Integer
    
    visibleRecipients = 0
    invisibleRecipients = 0
    
    '' to hide error if item not added to collection that we try to index via domain key - lazy?
    On Error Resume Next
    
    '' add all domain names which aren't the same as the senders to a collection so we can easily count the unique ones
    For Each Recipient In Item.Recipients
          parts = Split(Recipient.Address, "@")
          If UBound(parts) = 1 And parts(1) <> ownDomainName Then
            existing = ""
            
            If Recipient.Type = olBCC Then
              invisibleRecipients = invisibleRecipients + 1
            Else
              visibleRecipients = visibleRecipients + 1
            End If
          
            If domains.Count > 0 Then
                '' if not present then error handling will ignore the error
                existing = domains(parts(1))
            End If
            If existing = "" Then
                domains.Add parts(1), parts(1)
            End If
          End If
    Next
    
    If domains.Count > 1 And visibleRecipients > 0 Then '' to not warn when all in BCC, add "And visibleRecipients > 0"
        Dim externalDomains, message As String
        For Each domain In domains
            externalDomains = externalDomains + " " + domain
        Next
        
        message = "This email is being sent to " + CStr(domains.Count) + " different external domains:" + vbCrLf + vbCrLf + externalDomains + vbCrLf + vbCrLf + CStr(visibleRecipients) + " will be visible, " + CStr(invisibleRecipients) + " will not be visible" + vbCrLf + vbCrLf + "Continue?"
        answer = MsgBox(message, vbExclamation + vbYesNo, "Multiple Recipient Domains Detected")
        If answer <> vbYes Then
            Cancel = True
        End If
    End If
    
    Set olNS = Nothing
    Set olApp = Nothing
End Sub

